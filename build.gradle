plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.30"
}

ext {
    def correctVersion = project.hasProperty('versionNumber') && property('versionNumber') ==~ /\d+\.\d+\.\d+.*/
    versionNumber = correctVersion ? property('versionNumber') : 'SNAPSHOT-' + new Date().format('yyyyMMddHHmmss')
    projectIds = ['group': 'teamcity-google-agent', 'version': versionNumber]

    teamcityVersion = project.findProperty('teamcityVersion') ?: '2017.1'
    teamcityDir = project.findProperty('teamcityDir') ?: "$rootDir/servers/TeamCity-${teamcityVersion}"
    if (teamcityDir == null || !file(teamcityDir).isDirectory()) {
        throw new InvalidUserDataException('Please, execute gradle task `:teamcity-symbol-server:installTeamCity` ' +
                'or specify correct `teamcityDir` path in gradle.properties file')
    }
    teamcityDir = teamcityDir.replace("\\", "/")
    if (teamcityDir.endsWith("/")) {
        teamcityDir = teamcityDir.substring(0, teamcityDir.length() - 5)
    }
    println "TeamCity directory: " + teamcityDir
}

group = projectIds.group
version = projectIds.version

allprojects {
    group = projectIds.group
    version = projectIds.version
}

subprojects {
    apply plugin: "kotlin"

    test.useTestNG()
    jar.version = null
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.2.1'
}
